IsCalibrated
{
	_index = ASEARCH(_argv[0],GhostCalibrations.Name)
	if _index != -1
	{
		_list = GhostCalibrations.List[_index]
		if _list != ""
		{
			1
			return
		}
	}
	0
}

CalibrationIndex
{
	ASEARCH(_argv[0],GhostCalibrations.Name)
}

Flag
{
	if ASEARCH(_argv[0],Flags) != -1; 1
	else; 0
}

OnAddFlag
{
	AddFlag(reference0)
}

AddFlag
{
	if ASEARCH(_argv[0],Flags) == -1; Flags ,= _argv[0]
}

DeleteFlag
{
	_loop = 0
	while ASEARCH(_argv[0],Flags) != -1
	{
		_i = ASEARCH(_argv[0],Flags)
		Flags[_i] = IARRAY
		_loop++
		if _loop >= 1000; break
	}
}

Heading
{
	"\f[align,center]\f[bold,1]%(_argv[0])\f[bold,default]\n\f[align,left]"
}

FaceFade : all
{
	for _i = 0; _i < 9; _i++
	{
		"\b[110%(_i)0,--fallback=0]\_w[200]"
	}
	"\b[10000,--fallback=0]"
}

ControlLevel
{
	_ghost = TalkingGhost
	_index = CalibrationIndex(_ghost)
	
	if _index == -1; "Uncalibrated"
	else
	{
		_list = GhostCalibrations.List[_index]
		
		if _list == "none"; "No control"
		else
		{
			_i = 0
			foreach _list; _surface
			{
				if _surface != -1; _i++
			}
			
			case _i
			{
				when 0; "No control"
				when 1-4; "Minimal control"
				when 5-8; "Medium control"
				others; "Good control"
			}
		}
	}
}