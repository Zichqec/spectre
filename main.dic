PluginLoad
{
	LastTalkTime = GETSECCOUNT
	if !ISVAR("BootedGhosts"); BootedGhosts = IARRAY
	if !ISVAR("aitalkinterval"); aitalkinterval = 900
	
	if !ISVAR("GhostCalibrations.Name"); GhostCalibrations.Name = IARRAY
	if !ISVAR("GhostCalibrations.Setting"); GhostCalibrations.Setting = IARRAY
	if !ISVAR("GhostCalibrations.List"); GhostCalibrations.List = IARRAY
	if !ISVAR("Flags"); Flags = IARRAY
	if !ISVAR("ExcludedGhosts"); ExcludedGhosts = IARRAY
	if !ISVAR("MetSpectre"); MetSpectre = 0
	if !ISVAR("SpectreOn"); SpectreOn = 1
	
	YesterdaysPool = TodaysPool
	_pools = "dissociated,miserable,cheerful,cheerful2"
	TodaysPool = ANY(_pools)
	_i = 0
	while TodaysPool == YesterdaysPool
	{
		TodaysPool = ANY(_pools)
		_i++
		if _i > 1000; break
	}
	
	GreetedToday = 0
	ReachedOut = 5 //Adjust if desired
	ReachedOutTime = GETSECCOUNT
}

PluginUnload
{
	BootedGhosts = IARRAY
}

OnGhostBoot
{
	BootedGhosts ,= reference[1]
}

OnGhostExit
{
	_index = ASEARCH(reference[1],BootedGhosts)
	BootedGhosts[_index] = IARRAY
}

OnSecondChange
{
	if SpectreOn
	{
		if GETSECCOUNT >= LastTalkTime + aitalkinterval
		{
			OnAiTalk
		}
	}
	if GETSECCOUNT >= ReachedOutTime + 300; ReachedOut = 5
}

OnAiTalk
{
	LastTalkTime = GETSECCOUNT
	if reference[0] == ""
	{
		_possible_ghosts = IARRAY
		foreach BootedGhosts; _ghost
		{
			if ASEARCH(_ghost,ExcludedGhosts) == -1
			{
				_possible_ghosts ,= _ghost
			}
		}
		if ARRAYSIZE(_possible_ghosts) == 0; _possible_ghosts = BootedGhosts
		res_target = ANY(_possible_ghosts)
	}
	else
	{
		res_target = reference[0]
	}
	TalkingGhost = res_target
	res_marker = eventfrom
	res_script_option = "nobreak"
	
	if Flag("Stage 3") && TodaysPool == "dissociated" || !Flag("First boot seen"); GreetedToday = 1 //this feels wibbly wobbly
	if !GreetedToday //"boot" dialogues
	{
		if Flag("All boots complete"); LastTalk = Boot.Normal
		elseif Flag("Stage 1") && Flag("Second boot complete"); LastTalk = Boot.Returned
		elseif Flag("Stage 1"); LastTalk = Boot.Greeted
		elseif Flag("Stage 0") && Flag("First boot seen"); LastTalk = Boot.Greeted
	}
	else
	{
		if Flag("Stage 3"); LastTalk = Stage3Talk
		elseif Flag("Stage 2"); LastTalk = Stage2Talk
		elseif Flag("Stage 1"); LastTalk = Stage1Talk
		else
		{
			LastTalk = Stage0Talk
			//LastTalk = "\0%(blank)" + LastTalk + "%(end)" //think i dont need this...
		}
	}
	LastTalk = AutoPause(LastTalk)
	LastTalk = "\![no-autopause]\![timerraise,0,1,OnSpectrePlugin.Possession]" + LastTalk
	LastTalk
}

//writing this again cuz i'll die without it... and hey! what do you know! YAYA as PLUGIN doesn't have OnTranslateInternal...
AutoPause
{
	_talk = _argv[0]
	_talk = REPLACE(_talk,"\n\n ","\n\n\w8\w8")
	_talk = REPLACE(_talk,"... ","...\w8\w8 ")
	_talk = REPLACE(_talk,"...! ","...!\w8\w8 ")
	_talk = REPLACE(_talk,"...? ","...?\w8\w8 ")
	_talk = REPLACE(_talk,". ",".\w8\w8 ")
	_talk = REPLACE(_talk,"! ","!\w8\w8 ")
	_talk = REPLACE(_talk,"? ","?\w8\w8 ")
	_talk = REPLACE(_talk,"; ",";\w8 ")
	_talk = REPLACE(_talk,", ",",\w4 ")
	_talk
}

pluginpathlist
{
	foreach reference; _ref
	{
		_files = FENUM(_ref,"|")
		_files = SPLIT(_files,"|")
		_index = ASEARCH("\spectre_gz",_files)
		if _index != -1
		{
			SpectreFilePath = _ref + ERASE(_files[_index],0,1) + "\"
			return
		}
	}
}

version
{
	"Spectre v1.0.0"
}

eventfrom
{
	"Event from Spectre(plugin)"
}