PluginLoad
{
	LastTalkTime = GETSECCOUNT
	if !ISVAR("BootedGhosts"); BootedGhosts = IARRAY
	if !ISVAR("aitalkinterval"); aitalkinterval = 900
	
	if !ISVAR("GhostCalibrations.Name"); GhostCalibrations.Name = IARRAY
	if !ISVAR("GhostCalibrations.Setting"); GhostCalibrations.Setting = IARRAY
	if !ISVAR("GhostCalibrations.List"); GhostCalibrations.List = IARRAY
	if !ISVAR("Flags"); Flags = IARRAY
	if !ISVAR("ExcludedGhosts"); ExcludedGhosts = IARRAY
	if !ISVAR("MetSpectre"); MetSpectre = 0
	if !ISVAR("SpectreOn"); SpectreOn = 1
}

PluginUnload
{
	BootedGhosts = IARRAY
}

//How does this behave with duplicates? should figure that out
OnGhostBoot
{
	BootedGhosts ,= reference[1]
}

OnGhostExit
{
	_index = ASEARCH(reference[1],BootedGhosts)
	BootedGhosts[_index] = IARRAY
}

OnSecondChange
{
	if SpectreOn
	{
		if GETSECCOUNT >= LastTalkTime + aitalkinterval
		{
			OnAiTalk
		}
	}
}

OnAiTalk
{
	LastTalkTime = GETSECCOUNT
	if reference[0] == ""
	{
		_possible_ghosts = IARRAY
		foreach BootedGhosts; _ghost
		{
			if ASEARCH(_ghost,ExcludedGhosts) == -1
			{
				_possible_ghosts ,= _ghost
			}
		}
		if ARRAYSIZE(_possible_ghosts) == 0; _possible_ghosts = BootedGhosts
		res_target = ANY(_possible_ghosts)
	}
	else
	{
		res_target = reference[0]
	}
	TalkingGhost = res_target
	res_marker = eventfrom
	res_script_option = "nobreak"
	
	LastTalk = AutoPause(RandomTalk)
	LastTalk = "\![no-autopause]" + LastTalk + "\![timerraise,15000,1,OnSpectrePlugin.Possession]"
	LastTalk
}

//writing this again cuz i'll die without it... and hey! what do you know! YAYA as PLUGIN doesn't have OnTranslateInternal...
AutoPause
{
	_talk = _argv[0]
	_talk = REPLACE(_talk,"... ","...\w8\w8 ")
	_talk = REPLACE(_talk,"...! ","...!\w8\w8 ")
	_talk = REPLACE(_talk,"...? ","...?\w8\w8 ")
	_talk = REPLACE(_talk,". ",".\w8\w8 ")
	_talk = REPLACE(_talk,"! ","!\w8\w8 ")
	_talk = REPLACE(_talk,"? ","?\w8\w8 ")
	_talk = REPLACE(_talk,"; ",";\w8 ")
	_talk = REPLACE(_talk,", ",",\w4 ")
	_talk
}

version
{
	"Spectre v1.0.0"
}

eventfrom
{
	"Event from Spectre(plugin)"
}