PluginLoad
{
	LastTalkTime = GETSECCOUNT
	LastTalk = ""
	if !ISVAR("BootedGhosts"); BootedGhosts = IARRAY
	if !ISVAR("aitalkinterval"); aitalkinterval = 900
	
	if !ISVAR("GhostCalibrations.Name"); GhostCalibrations.Name = IARRAY
	if !ISVAR("GhostCalibrations.Setting"); GhostCalibrations.Setting = IARRAY
	if !ISVAR("GhostCalibrations.List"); GhostCalibrations.List = IARRAY
	if !ISVAR("Flags"); Flags = IARRAY
	if !ISVAR("ExcludedGhosts"); ExcludedGhosts = IARRAY
	if !ISVAR("SpectreOn"); SpectreOn = 1
	
	YesterdaysPool = TodaysPool
	_pools = "dissociated,miserable,cheerful,cheerful2"
	TodaysPool = ANY(_pools)
	_i = 0
	while TodaysPool == YesterdaysPool
	{
		TodaysPool = ANY(_pools)
		_i++
		if _i > 1000; break
	}
	
	GreetedToday = 0
	ReachedOut = 5 //Adjust if desired
	ReachedOutTime = GETSECCOUNT
}

PluginUnload
{
	BootedGhosts = IARRAY
}

OnGhostBoot
{
	BootedGhosts ,= reference[1]
	
	//This, tragically, interrupts boot events...
	// if !IsCalibrated(reference[1])
	// {
		// res_target = reference[1]
		// CalibrationConfirm = "OnGhostBoot"
		
		// "\![raise,OnSpectrePlugin.ConfirmCalibration]"
	// }
}

OnGhostExit
{
	_index = ASEARCH(reference[1],BootedGhosts)
	BootedGhosts[_index] = IARRAY
}

OnSecondChange
{
	if SpectreOn
	{
		if GETSECCOUNT >= LastTalkTime + aitalkinterval
		{
			OnSpectreTalk
		}
	}
	if GETSECCOUNT >= ReachedOutTime + 300; ReachedOut = 5
}

OnSpectreTalk
{
	LastTalkTime = GETSECCOUNT
	
	if ARRAYSIZE(DialogueDisperser) == 0
	{
		DialogueDisperser = ("reg","reg","reg","reg","reg","reg","reg","question")
		DialogueDisperser = ArrayShuffle(DialogueDisperser)
	}
	_isquestion = 0
	if DialogueDisperser[0] == "question"; _isquestion = 1
	DialogueDisperser[0] = IARRAY
	
	DetermineTalkTarget(reference[0])
	
	if Flag("Stage 3") && TodaysPool == "dissociated" || !Flag("Stage 1"); GreetedToday = 1 //this feels wibbly wobbly
	if !MetSpectre && Flag("Stage 0 greeted") //Not met but did greet
	{
		LastTalk = Boot.Greeted
	}
	elseif !GreetedToday //"boot" dialogues
	{
		if Flag("Stage 2"); LastTalk = Boot.Normal
		elseif Flag("Stage 1") && Flag("Second boot complete"); LastTalk = Boot.Returned
		elseif Flag("Stage 1"); LastTalk = Boot.Greeted
	}
	else
	{
		if Flag("Stage 3"); LastTalk = Stage3Talk
		elseif Flag("Stage 2")
		{
			if _isquestion; LastTalk = Stage2Questions
			else; LastTalk = Stage2Talk
		}
		elseif Flag("Stage 1")
		{
			if _isquestion; LastTalk = Stage1Questions
			else; LastTalk = Stage1Talk
		}
		else
		{
			AddFlag("Spectre seen")
			LastTalk = Stage0Talk
			//LastTalk = "\0%(blank)" + LastTalk + "%(end)" //think i dont need this...
		}
	}
	LastTalk = AutoPause(LastTalk)
	LastTalk = "\![no-autopause]\![timerraise,0,1,OnSpectrePlugin.Possession]" + LastTalk
	LastTalk
}

ArrayShuffle
{
	_array = _argv
	_output = IARRAY
	while ARRAYSIZE(_array) > 0
	{
		_output ,= ANY(_array)
		_array[LSO] = IARRAY
	}
	_output
}

DetermineTalkTarget
{
	if _argv[0] == ""
	{
		_possible_ghosts = IARRAY
		foreach BootedGhosts; _ghost
		{
			if ASEARCH(_ghost,ExcludedGhosts) == -1
			{
				_possible_ghosts ,= _ghost
			}
		}
		if ARRAYSIZE(_possible_ghosts) == 0; _possible_ghosts = BootedGhosts
		res_target = ANY(_possible_ghosts)
	}
	else
	{
		res_target = reference[0]
	}
	TalkingGhost = res_target
	res_marker = eventfrom
	res_script_option = "nobreak"
}

Capitalize
{
	TOUPPER(SUBSTR(_argv[0],0,1)) + ERASE(_argv[0],0,1)
}

//writing this again cuz i'll die without it... and hey! what do you know! YAYA as PLUGIN doesn't have OnTranslateInternal...
AutoPause
{
	_talk = _argv[0]
	_talk = REPLACE(_talk,'?" ','?"\w8\w8 ')
	_talk = REPLACE(_talk,'!" ','!"\w8\w8 ')
	_talk = REPLACE(_talk,'." ','."\w8\w8 ')
	_talk = REPLACE(_talk,".' ",".'\w8\w8 ")
	_talk = REPLACE(_talk,"?' ","?'\w8\w8 ")
	_talk = REPLACE(_talk,"!' ","!'\w8\w8 ")
	_talk = REPLACE(_talk,"\n\n ","\n\n\w8\w8")
	_talk = REPLACE(_talk,"%(emdash) ","%(emdash)\w8 ")
	_talk = REPLACE(_talk,"... ","...\w8\w8 ")
	_talk = REPLACE(_talk,"...! ","...!\w8\w8 ")
	_talk = REPLACE(_talk,"...? ","...?\w8\w8 ")
	_talk = REPLACE(_talk,". ",".\w8\w8 ")
	_talk = REPLACE(_talk,"! ","!\w8\w8 ")
	_talk = REPLACE(_talk,"? ","?\w8\w8 ")
	_talk = REPLACE(_talk,"; ",";\w8 ")
	_talk = REPLACE(_talk,", ",",\w4 ")
	_talk
}

pluginpathlist
{
	foreach reference; _ref
	{
		_files = FENUM(_ref,"|")
		_files = SPLIT(_files,"|")
		_index = ASEARCH("\spectre_gz",_files)
		if _index != -1
		{
			SpectreFilePath = _ref + ERASE(_files[_index],0,1) + "\"
			return
		}
	}
}

version
{
	"Spectre v1.0.2"
}

eventfrom
{
	"Event from Spectre(plugin)"
}